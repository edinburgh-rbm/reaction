##
## Extracts a simple contact graph from the RDF representation
## of Kappa rules
##

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rbmo: <http://purl.org/rbm/rbmo/>

SELECT DISTINCT ?rule ?contact_agent ?contact_site WHERE {

   ## the left hand side of a rule has an agent 
   ## with a site bound to nothing
   ?rule rbmo:lhs [
       rbmo:pattern [
           rbmo:agent ?agent;
           rbmo:site [
              rbmo:binding rbmo:nothing;
              dct:identifier ?site
           ]
       ]
   ] .

   ## the right hand side of a rule has the same
   ## agent with the site bound to something.
   ?rule rbmo:rhs [
       rbmo:pattern [
           rbmo:agent ?agent;
           rbmo:site [
              rbmo:binding ?binding;
              dct:identifier ?site
           ]
       ]
   ] .

   ## now we find the agents, and sites on the right hand 
   ## side who have become bound
   ?rule rbmo:rhs [
       rbmo:pattern [
           rbmo:agent ?contact_agent;
           rbmo:site [
               rbmo:binding ?binding;
               dct:identifier ?contact_site;
           ]
       ]
   ] .

   ## this filter is necessary to check that the binding
   ## actually is one, that is blank nodes are used to
   ## bind sites
   FILTER isBlank(?binding)
}