##
## Extracts a simple contact graph from the RDF representation
## of Kappa rules
##

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rbmo: <http://purl.org/rbm/rbmo/>

SELECT DISTINCT ?rule ?contact_agent ?contact_site WHERE {

   ## the left hand side of a rule has an agent 
   ## with a site bound to nothing
   ?rule rbmo:lhs [
       rbmo:configurationOf ?agent;
       rbmo:hasState [
          rbmo:binding rbmo:nothing;
          rbmo:stateOf ?site
       ]
   ] .

   ## the right hand side of a rule has the same
   ## agent with the site bound to something.
   ?rule rbmo:rhs [
       rbmo:configurationOf ?agent;
       rbmo:hasState [
          rbmo:binding ?binding;
          rbmo:stateOf ?site
       ]
   ] .

   ## now we find the agents, and sites on the right hand 
   ## side who have become bound
   ?rule rbmo:rhs [
       rbmo:configurationOf ?contact_agent;
       rbmo:hasState [
           rbmo:binding ?binding;
           rbmo:stateOf ?contact_site;
       ]
   ] .

   ## this filter is necessary to check that the binding
   ## actually is one, that is blank nodes are used to
   ## bind sites
   FILTER isBlank(?binding)
}