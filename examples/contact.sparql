##
## Extracts a simple contact graph from the RDF representation
## of Kappa rules
##

PREFIX rbmo: <http://purl.org/rbm/rbmo/>

SELECT DISTINCT ?rule ?contact_agent ?contact_site WHERE {

   ## the left hand side of a rule has an agent 
   ## with a site bound to nothing
   ?rule rbmo:lhs [
       rbmo:agent ?agent;
       rbmo:state [
          rbmo:binding rbmo:Nothing;
          rbmo:site ?site
       ]
   ] .

   ## the right hand side of a rule has the same
   ## agent with the site bound to something.
   ?rule rbmo:rhs [
       rbmo:agent ?agent;
       rbmo:state [
          rbmo:binding ?binding;
          rbmo:site ?site
       ]
   ] .

   ## now we find the agents, and sites on the right hand 
   ## side who have become bound
   ?rule rbmo:rhs [
       rbmo:agent ?contact_agent;
       rbmo:state [
           rbmo:binding ?binding;
           rbmo:site ?contact_site;
       ]
   ] .

   ## this filter is necessary to check that the binding
   ## actually is one, that is blank nodes are used to
   ## bind sites
   FILTER isBlank(?binding)
}